apply plugin: 'com.android.application'

def appName = "AppManage" // APP名称

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 3
        versionName "3.0"
        applicationId "com.henley.appmanage"
    }

    signingConfigs { // 包签名配置
        release {
            storeFile file('appmanage.jks')
            storePassword '123456'
            keyAlias 'AppManage'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled true // 是否进行代码混淆
            zipAlignEnabled true // 是否进行Zip压缩优化
            shrinkResources true // 是否移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (!variant.buildType.debuggable) {
                output.assemble.doLast {
                    copy {
                        def targetName = "${appName}-${variant.versionName}.apk"
                        from output.outputFile
                        into project.rootDir
                        rename { String fileName ->
                            fileName.replace("app-release.apk", targetName)
                        }
                        System.out.println("generate ${targetName} success in dir ${new File(project.rootDir, targetName)}.")
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:support-fragment:28.0.0'
    implementation 'com.android.design:MaterialDialog:1.1.5'
}